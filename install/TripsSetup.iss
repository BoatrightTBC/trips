; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Trips"
#define MyAppVersion "0.65"
#define MyAppPublisher "Team Gannon"
#define MyAppURL "https://charlesegannon.com/wp/my-worlds/terran-republic/"
#define MyAppExeName "TripsRun.bat"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{43FC7AD3-B865-4F95-80BF-212BFF2E55D0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=\{#MyAppName}
DefaultGroupName={#MyAppName}
;LicenseFile=C:\TripsSafe\License.rtf
InfoBeforeFile=C:\Users\rick\Documents\GitHub\trips\install\Pre-Install.rtf
InfoAfterFile=C:\Users\rick\Documents\GitHub\trips\install\PostInstall.rtf
OutputDir=C:\TripsSafe\Setups
OutputBaseFilename=TripsInstall
SetupIconFile=C:\TRIPS\trips.ico
Compression=lzma
SolidCompression=yes
AlwaysShowComponentsList=yes
ShowComponentSizes=No
FlatComponentsList=False
PrivilegesRequired=none
RestartIfNeededByRun=False
ShowLanguageDialog=no
LanguageDetectionMethod=none
AppReadmeFile=C:\TripsSafe\readme.md
VersionInfoVersion=0.65
VersionInfoCompany=Team Gannon
VersionInfoDescription=Terran Republic Info for Plotting Stars
VersionInfoCopyright=2020, 2021 Charles E Gannon
VersionInfoProductName=TRIPS
MinVersion=0,6.1

[InstallDelete]
Type: files; Name: "{app}\data\tripsdb.mv.db"

[Types]
Name: "full"; Description: "Install program and download sample data"; Flags: iscustom
Name: "base"; Description: "Install program only"

[Components]
Name: "program"; Description: "Program Files"; Types: full base; Flags: fixed
Name: "data"; Description: "Sample Data Files"; Types: full
Name: "data\small"; Description: "Sample data 70 LY sphere"
Name: "data\big"; Description: "270 ly diameter big sample data"
Name: "data\chv"; Description: "Old ChView data files"

[Files]
Source: "C:\TRIPS\TripsRun.bat"; DestDir: "{app}"; Components: program
Source: "C:\TRIPS\trips.jar"; DestDir: "{app}"; Flags: ignoreversion; Components: program
Source: "C:\TRIPS\trips.ico"; DestDir: "{app}"; Components: program
Source: "C:\TRIPS\jre\*"; DestDir: "{app}\jre"; Flags: recursesubdirs ignoreversion; Components: program
Source: "C:\TRIPS\javafx-sdk-15\*"; DestDir: "{app}\javafx-sdk-15"; Flags: recursesubdirs ignoreversion; Components: program
Source: "C:\TRIPS\data\*"; DestDir: "{app}\data"; Flags: recursesubdirs ignoreversion; Components: program; Excludes: "*.db"
Source: "{tmp}\Trips-normal-names-70.xlsx"; DestDir: "{app}\files\Excel"; ExternalSize: 1945600 ; Flags: external; Components: data\small
Source: "{tmp}\Trips-normal-names-270.xlsx"; DestDir: "{app}\files\Excel"; ExternalSize: 98926592; Components: data\big; Flags: external
Source: "{tmp}\25LY-H.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 26264 ; Components: data\chv
Source: "{tmp}\100LY-H.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 476160 ; Components: data\chv
Source: "{tmp}\SOPHANTS.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 476160 ; Components: data\chv
Source: "{tmp}\TERRAGRP.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 15360 ; Components: data\chv

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\trips.ico"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; IconFilename: "{app}\myicon.ico"
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon; IconFilename: "{app}\myicon.ico"

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

[Dirs]
Name: "{app}\files"
Name: "{app}\files\Excel"
Name: "{app}\file\ChView"
Name: "{app}\files\csv"
Name: "{app}\data"
Name: "{app}\jre"
Name: "{app}\javafx-sdk-15"
Name: "{app}\logs"

[Code]
var
  DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    if WizardIsComponentSelected('data\small') then begin
      DownloadPage.Add('https://github.com/BoatrightTBC/trips/raw/develop/files/Excel/Trips-normal-names-70.xlsx', '70LY data', '');
    end;
    if WizardIsComponentSelected('data\big') then begin
      DownloadPage.Add('https://github.com/BoatrightTBC/trips/raw/develop/files/Excel/Trips-normal-names-270.xlsx', '270LY data', '');
    end;
    if WizardIsComponentSelected('data\chv') then begin
      DownloadPage.Add('https://github.com/BoatrightTBC/trips/raw/develop/files/ChView/25LY-H.CHV', '25LY-H.CHV', '');
      DownloadPage.Add('https://github.com/BoatrightTBC/trips/raw/develop/files/ChView/100LY-H.CHV', '100LY-H.CHV', '');
      DownloadPage.Add('https://github.com/BoatrightTBC/trips/raw/develop/files/ChView/SOPHANTS.CHV', 'SOPHANTS.CHV', '');
      DownloadPage.Add('https://github.com/BoatrightTBC/trips/raw/develop/files/ChView/TERRAGRP.CHV', 'TERRAGRP.CHV', '');
    end;
    DownloadPage.Show;
    try
      try
        DownloadPage.Download;
        Result := True;
      except
        SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;