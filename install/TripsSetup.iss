; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Trips"
#define MyAppVersion "0.66"
#define MyAppPublisher "Team Gannon"
#define MyAppURL "https://charlesegannon.com/wp/my-worlds/terran-republic/"
#define MyAppExeName "TripsRun.bat"
#define VCLStyle "Windows10.vsf"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{43FC7AD3-B865-4F95-80BF-212BFF2E55D0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=\{#MyAppName}
DefaultGroupName={#MyAppName}
;LicenseFile=C:\TripsSafe\License.rtf
InfoBeforeFile=C:\Users\rick\Documents\GitHub\trips\install\Pre-Install.rtf
InfoAfterFile=C:\Users\rick\Documents\GitHub\trips\install\PostInstall.rtf
OutputDir=C:\TripsSafe\Setups
OutputBaseFilename=TripsInstall
SetupIconFile=C:\Users\rick\Documents\GitHub\trips\tripsWin.ico
Compression=lzma
SolidCompression=yes
AlwaysShowComponentsList=yes
ShowComponentSizes=No
FlatComponentsList=False
PrivilegesRequired=none
RestartIfNeededByRun=False
ShowLanguageDialog=no
LanguageDetectionMethod=none
DisableWelcomePage=no
AppReadmeFile=C:\Users\rick\Documents\GitHub\trips\readme.md
VersionInfoVersion=0.66
VersionInfoCompany=Team Gannon
VersionInfoDescription=Terran Republic Interstellar Plotting System
VersionInfoCopyright=2020, 2021 Charles E Gannon
VersionInfoProductName=TRIPS
MinVersion=0,6.1
WizardImageFile=images\WizModernImage-IS.bmp
WizardSmallImageFile=images\WizModernSmallImage-IS.bmp

[InstallDelete]
Type: files; Name: "{app}\data\tripsdb.mv.db"

[Types]
Name: "full"; Description: "Install program and download sample data"; Flags: iscustom
Name: "base"; Description: "Install program only"

[Components]
Name: "program"; Description: "Program Files"; Types: full base; Flags: fixed
Name: "data"; Description: "Sample Data Files"; Types: full
Name: "data\small"; Description: "Sample data 70 LY sphere"
Name: "data\big"; Description: "270 ly diameter big sample data"
Name: "data\chv"; Description: "Old ChView data files"

[Files]
Source: VclStylesinno.dll; DestDir: {app}; Flags: dontcopy
Source: {#VCLStyle}; DestDir: {app}; Flags: dontcopy
Source: "./ReleaseNotes_0.66.pdf"; DestDir: "{app}"; Components: program
Source: "C:\TRIPS\TripsRun.bat"; DestDir: "{app}"; Components: program
Source: "C:\TRIPS\trips.jar"; DestDir: "{app}"; Flags: ignoreversion; Components: program
Source: "C:\TRIPS\tripsWin.ico"; DestDir: "{app}"; Components: program
Source: "C:\TRIPS\jre\*"; DestDir: "{app}\jre"; Flags: recursesubdirs ignoreversion; Components: program
Source: "C:\TRIPS\javafx-sdk-15\*"; DestDir: "{app}\javafx-sdk-15"; Flags: recursesubdirs ignoreversion; Components: program
Source: "C:\TRIPS\data\*"; DestDir: "{app}\data"; Flags: recursesubdirs ignoreversion; Components: program; Excludes: "*.db"
Source: "{tmp}\Trips-normal-names-70.xlsx"; DestDir: "{app}\files\Excel"; ExternalSize: 1945600 ; Flags: external; Components: data\small
Source: "{tmp}\Trips-normal-names-270.xlsx"; DestDir: "{app}\files\Excel"; ExternalSize: 98926592; Components: data\big; Flags: external
Source: "{tmp}\25LY-H.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 26264 ; Components: data\chv
Source: "{tmp}\100LY-H.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 476160 ; Components: data\chv
Source: "{tmp}\SOPHONTS.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 476160 ; Components: data\chv
Source: "{tmp}\TERRAGRP.CHV"; DestDir: "{app}\files\ChView"; Flags: external; ExternalSize: 15360 ; Components: data\chv

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\trips.ico"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; IconFilename: "{app}\myicon.ico"
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon; IconFilename: "{app}\myicon.ico"

[Run]
Filename: "{app}\ReleaseNotes_0.66.pdf"; Description: "Release notes for version 0.66"; Flags: postinstall shellexec skipifsilent; Verb: "open" 
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

[Dirs]
Name: "{app}\files"
Name: "{app}\files\Excel"
Name: "{app}\file\ChView"
Name: "{app}\files\csv"
Name: "{app}\data"
Name: "{app}\jre"
Name: "{app}\javafx-sdk-15"
Name: "{app}\logs"

[Code]
//Import the LoadVCLStyle function from VclStylesInno.DLL
procedure LoadVCLStyle(VClStyleFile: String); external 'LoadVCLStyleW@files:VclStylesInno.dll stdcall';
// Import the UnLoadVCLStyles function from VclStylesInno.DLL
procedure UnLoadVCLStyles; external 'UnLoadVCLStyles@files:VclStylesInno.dll stdcall';

function InitializeSetup(): Boolean;
begin
  ExtractTemporaryFile('{#VCLStyle}');
  LoadVCLStyle(ExpandConstant('{tmp}\{#VCLStyle}'));
  Result := True;
end;

procedure DeinitializeSetup();
begin
	UnLoadVCLStyles;
end;

var
  DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    if WizardIsComponentSelected('data\small') then begin
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/Trips-normal-names-70.xlsx', 'Trips-normal-names-70.xlsx', '');
    end;
    if WizardIsComponentSelected('data\big') then begin
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/Trips-normal-names-70.xlsx', 'Trips-normal-names-270.xlsx', '');
    end;
    if WizardIsComponentSelected('data\chv') then begin
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/25LY-H.CHV', '25LY-H.CHV', '');
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/100LY-H.CHV', '100LY-H.CHV', '');
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/SOPHONTS.CHV', 'SOPHONTS.CHV', '');
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/TERRAGRP.CHV', 'TERRAGRP.CHV', '');
    end;
    DownloadPage.Show;
    try
      try
        DownloadPage.Download;
        Result := True;
      except
        SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;