; Script generated by the Inno Script Studio Wizard.

#define MyAppName "Trips"
#define MyAppVersion "0.66"
#define MyAppPublisher "Team Gannon"
#define MyAppURL "https://charlesegannon.com/wp/my-worlds/terran-republic/"
#define MyAppExeName "TripsRun.bat"
#define VCLStyle "Windows10.vsf"
#define MyAppSrc "C:\TRIPS\"

[Setup]
; App ID Stuff
AppId={{43FC7AD3-B865-4F95-80BF-212BFF2E55D0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=\{#MyAppName}
DefaultGroupName={#MyAppName}

; Setup File stuff
InfoBeforeFile=Pre-Install.rtf
InfoAfterFile=PostInstall.rtf
SetupIconFile=tripsWin.ico
WizardImageFile=images\WizModernImage-IS.bmp
WizardSmallImageFile=images\WizModernSmallImage-IS.bmp

; Output stuff
OutputDir=C:\TripsSafe\Setups
OutputBaseFilename=TripsInstall{#MyAppVersion}
AppReadmeFile=..\readme.md

; Options
Compression=lzma
SolidCompression=yes
AlwaysShowComponentsList=yes
DisableWelcomePage=no
ShowComponentSizes=yes
FlatComponentsList=no
PrivilegesRequired=none
RestartIfNeededByRun=no
ShowLanguageDialog=no
LanguageDetectionMethod=none

; Version Info for details tab of the setup.exe program itself. 
VersionInfoVersion=0.66.1
VersionInfoCompany="Team Gannon"
VersionInfoDescription="Terran Republic Interstellar Plotting System"
VersionInfoCopyright="2020, 2021 Charles E Gannon"
VersionInfoProductName="TRIPS"

[InstallDelete]
Type: files; Name: "{app}\data\tripsdb.mv.db"

[Types]
Name: "full"; Description: "Install program and download sample data"; Flags: iscustom
Name: "base"; Description: "Install program only"

[Components]
Name: "program"; Description: "Program Files"; Types: full base; Flags: fixed
Name: "data";    Description: "Sample Data Files"; Types: full
Name: "data\small"; Description: "Sample data 70 LY sphere"
Name: "data\big";   Description: "Sample data 270 LY sphere"
Name: "data\chv";   Description: "Old ChView data files"

[Files]
; Inno Setup Style files
Source: VclStylesinno.dll; DestDir: {app}; Flags: dontcopy
Source: {#VCLStyle};       DestDir: {app}; Flags: dontcopy

; Actual files to install 
Source: "{#MyAppSrc}\TripsRun.bat";    DestDir: "{app}"; Components: program
Source: "{#MYAppSrc}\tripsWin.ico";    DestDir: "{app}"; Components: program
Source: "{#MYAppSrc}\trips.jar";       DestDir: "{app}"; Components: program; Flags: ignoreversion
Source: "..\Releases\ReleaseNotes_0.66.pdf";    DestDir: "{app}"; Components: program

; Program Libraries
Source: "{#MYAppSrc}\javafx-sdk-15\*"; DestDir: "{app}\javafx-sdk-15"; Components: program; Flags: recursesubdirs ignoreversion
Source: "{#MYAppSrc}\jre\*";           DestDir: "{app}\jre";           Components: program; Flags: recursesubdirs ignoreversion
Source: "{#MYAppSrc}\data\*";          DestDir: "{app}\data";          Components: program; Flags: recursesubdirs ignoreversion; Excludes: "*.db"

; Optional Datasets for download 
Source: "{tmp}\Trips-normal-names-70.xlsx";  DestDir: "{app}\files\Excel"; ExternalSize:  1945600; Components: data\small; Flags: external
Source: "{tmp}\Trips-normal-names-270.xlsx"; DestDir: "{app}\files\Excel"; ExternalSize: 98926592; Components: data\big;   Flags: external
Source: "{tmp}\25LY-H.CHV"; DestDir: "{app}\files\ChView";   ExternalSize:   26264; Components: data\chv; Flags: external
Source: "{tmp}\100LY-H.CHV"; DestDir: "{app}\files\ChView";  ExternalSize:  476160; Components: data\chv; Flags: external;
Source: "{tmp}\SOPHONTS.CHV"; DestDir: "{app}\files\ChView"; ExternalSize: 476160 ; Components: data\chv; Flags: external
Source: "{tmp}\TERRAGRP.CHV"; DestDir: "{app}\files\ChView"; ExternalSize:  15360 ; Components: data\chv; Flags: external

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Icons]
Name: "{group}\{#MyAppName}";         Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\trips.ico"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; IconFilename: "{app}\myicon.ico"

[Run]
Filename: "{app}\ReleaseNotes_0.66.pdf"; Description: "Release notes for version 0.66"; Flags: postinstall shellexec; Verb: "open" 
Filename: "{app}\{#MyAppExeName}";       Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

[Dirs]
Name: "{app}\files"
Name: "{app}\files\Excel"
Name: "{app}\file\ChView"
Name: "{app}\files\csv"
Name: "{app}\data"
Name: "{app}\jre"
Name: "{app}\javafx-sdk-15"
Name: "{app}\logs"

[Code]
//Import the LoadVCLStyle function from VclStylesInno.DLL
procedure LoadVCLStyle(VClStyleFile: String); external 'LoadVCLStyleW@files:VclStylesInno.dll stdcall';
// Import the UnLoadVCLStyles function from VclStylesInno.DLL
procedure UnLoadVCLStyles; external 'UnLoadVCLStyles@files:VclStylesInno.dll stdcall';

function InitializeSetup(): Boolean;
begin
  ExtractTemporaryFile('{#VCLStyle}');
  LoadVCLStyle(ExpandConstant('{tmp}\{#VCLStyle}'));
  Result := True;
end;

procedure DeinitializeSetup();
begin
	UnLoadVCLStyles;
end;

var
  DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Successfully downloaded file to {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    if WizardIsComponentSelected('data\small') then begin
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/Trips-normal-names-70.xlsx', 'Trips-normal-names-70.xlsx', '');
    end;
    if WizardIsComponentSelected('data\big') then begin
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/Trips-normal-names-70.xlsx', 'Trips-normal-names-270.xlsx', '');
    end;
    if WizardIsComponentSelected('data\chv') then begin
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/25LY-H.CHV', '25LY-H.CHV', '');
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/100LY-H.CHV', '100LY-H.CHV', '');
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/SOPHONTS.CHV', 'SOPHONTS.CHV', '');
      DownloadPage.Add('https://tbcsoftware.com/trips/Datasets/CHV/TERRAGRP.CHV', 'TERRAGRP.CHV', '');
    end;
    DownloadPage.Show;
    try
      try
        DownloadPage.Download;
        Result := True;
      except
        SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;